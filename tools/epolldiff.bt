#!/usr/bin/env bpftrace
/*
 * epolldiff.bt	Check if events generated by epoll_wait are equal to maxevents.
 *		For Linux, uses bpftrace, eBPF.
 *
 *
 * Licensed under the Apache License, Version 2.0 (the "License")
 *
 * 09-Jun-2023	Tariro Mukute   Created this.
 */


BEGIN
{
	printf("Printing count for difference between maxevents and events generated. Also shows count of errors.  Hit Ctrl-C to end.\n");
    printf("Output format -> [maxevents, difference]: count")
}

t:syscalls:sys_enter_epoll_wait 
{
    @epoll_maxevent[tid] = args->maxevents;
}

t:syscalls:sys_exit_epoll_wait
/@epoll_maxevent[tid]/
{
    $m = @epoll_maxevent[tid];
    if(args->ret < 0) {
        @err[-args->ret] = count();
    } else {
        $d = $m - args->ret;
        @diff[$m, $d] = count();
    }
    delete(@epoll_maxevent[tid]);
}

END
{
    clear(@epoll_maxevent);
}
